"""IMPORTANT. Do this first to setup vim-plug
" 1. Follow instructions in https://github.com/junegunn/vim-plug#installation.
"    (put the plug.vim file in ~/.vim/autoload directory)
"""

"""IMPORTANT. If using neovim do the following.
" 1. Install neovim using `brew install neovim`
" 2. create ~/.config/nvim/init.vim with these contents (without the comment quotes):
"
"    set runtimepath^=~/.vim runtimepath+=~/.vim/after
"    let &packpath = &runtimepath
"    source ~/.vimrc
"
"""

"""IMPORTANT. For python stuff, make sure to install the following.
" 1. jedi: `pip install jedi`
" 2. flake8: `pip install flake8`
"""

"""IMPORTANT. Do this after finishing the above steps.
" 1. Open vim/nvim and run :PlugInstall
"""

let mapleader = ","
set nocompatible
filetype off

"""Start of Plug stuff
call plug#begin('~/.vim/plugged')
Plug 'davidhalter/jedi-vim'
Plug 'easymotion/vim-easymotion'
Plug 'ervandew/supertab'
Plug 'flazz/vim-colorschemes'
Plug 'guns/vim-sexp'
Plug 'haya14busa/incsearch-easymotion.vim'
Plug 'haya14busa/incsearch.vim'
Plug 'kien/rainbow_parentheses.vim'
Plug 'scrooloose/nerdtree'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-fireplace'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-salve'
Plug 'tpope/vim-sexp-mappings-for-regular-people'
Plug 'tpope/vim-surround'
Plug 'venantius/vim-cljfmt'
Plug 'venantius/vim-eastwood'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'vim-syntastic/syntastic'
call plug#end()
"""End of Plug stuff

filetype plugin indent on
syntax on

set tabstop=4
set expandtab
set shiftwidth=4
set tw=90
set mouse=a
set ic
set incsearch
set ruler
set nofoldenable

"""Color scheme
colorscheme jellybeans

"""Setup backup/swap dir
set backup
set swapfile
set backupdir=~/.vim-tmp
set directory=~/.vim-tmp

match ErrorMsg '\%>100v.\+'
match ErrorMsg '\s\+\%#\@<!$'
autocmd InsertLeave * redraw!

"""Syntastic
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 0
let g:syntastic_check_on_open = 0
let g:syntastic_check_on_wq = 0
highlight SyntasticError guibg=#2f0000
highlight SyntasticErrorSign guifg=white guibg=red
highlight SyntasticErrorLine guifg=white guibg=red
highlight SyntasticWarningLine guifg=white guibg=red
let g:syntastic_enable_signs = 1
nnoremap <leader>en :silent! lnext<CR>
nnoremap <leader>ep :silent! lprev<CR>

"""Rainbow parentheses everywhere
autocmd VimEnter * RainbowParenthesesToggle
autocmd Syntax * RainbowParenthesesLoadRound
autocmd Syntax * RainbowParenthesesLoadSquare
autocmd Syntax * RainbowParenthesesLoadBraces

"""Easymotion
map <Leader> <Plug>(easymotion-prefix)
nmap <Leader>, <Plug>(easymotion-overwin-f)
map  <Leader>f <Plug>(easymotion-bd-f)
nmap <Leader>f <Plug>(easymotion-overwin-f)
map <Leader>/ <Plug>(incsearch-easymotion-/)
map <Leader>? <Plug>(incsearch-easymotion-?)
map <Leader>l <Plug>(easymotion-lineforward)
map <Leader>j <Plug>(easymotion-j)
map <Leader>k <Plug>(easymotion-k)
map <Leader>h <Plug>(easymotion-linebackward)
let g:EasyMotion_startofline = 0 " keep cursor column when JK motion

"""C++
let g:syntastic_cpp_compiler = 'clang++'
let g:syntastic_cpp_compiler_options = ' -std=c++11 -stdlib=libc++'

"""Python
"(Remember to pip install flake8!)
let g:syntastic_python_checkers = ['flake8']
let g:syntastic_python_flake8_args = "--max-line-length=100"

"Jedi Vim
"(Remember to pip install jedi!)
let g:jedi#completions_enabled = 1
let g:jedi#popup_on_dot = 0 "Use ctrl-space to show completions instead because this can be slow
autocmd FileType python setlocal completeopt-=preview "Do not show popup doc

"""Clojure
autocmd Filetype clojure nmap <Leader>r :Require<cr>
autocmd FileType clojure setlocal completeopt-=preview  "Do not show popup doc
autocmd BufRead,BufNewFile *.cljs setlocal filetype=clojure
autocmd BufRead,BufNewFile *.boot setlocal filetype=clojure
let g:syntastic_clojure_checkers = ['eastwood']
let g:clj_fmt_autosave = 0  "Do not auto format; let me do it as needed with :Cljfmt
map <Leader>cc :Connect nrepl://localhost:9777<CR>

"""NERDTree
nnoremap <leader>nt :NERDTreeToggle<CR>

"""Tabs
nnoremap <leader>tn :silent! tabn<CR>
nnoremap <leader>tp :silent! tabp<CR>

"""Unhighlight last search on escape
nnoremap <Esc> :nohlsearch<CR><Esc>

"""Highlight rows which are too long
highlight OverLength ctermbg=red ctermfg=white guibg=#592929
match OverLength /\%101v.\+/

"""Highlight all tabs and trailing whitespace characters
highlight ExtraWhitespace ctermbg=white guibg=white
match ExtraWhitespace /\s\+$\|\t/

"""Set highlight color for search
highlight Search cterm=NONE ctermfg=black ctermbg=white

"""Custom functions
func! DeleteTrailingWS()
    exe "normal mz"
    %s/\s\+$//ge
    exe "normal 'z"
endfunc
autocmd BufWrite * :call DeleteTrailingWS()

"With cursor on line call :Underline
function! s:Underline(chars)
    let chars = empty(a:chars) ? '-' : a:chars
    let nr_columns = virtcol('$') - 1
    let uline = repeat(chars, (nr_columns / len(chars)) + 1)
    put =strpart(uline, 0, nr_columns)
endfunction
command! -nargs=? Underline call s:Underline(<q-args>)

"Auto create non-existent dirs when creating a new file
function s:MkNonExDir(file, buf)
    if empty(getbufvar(a:buf, '&buftype')) && a:file!~#'\v^\w+\:\/'
        let dir=fnamemodify(a:file, ':h')
        if !isdirectory(dir)
            call mkdir(dir, 'p')
        endif
    endif
endfunction
augroup BWCCreateDir
    autocmd!
    autocmd BufWritePre * :call s:MkNonExDir(expand('<afile>'), +expand('<abuf>'))
augroup END
